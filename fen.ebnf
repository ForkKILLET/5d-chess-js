(*
# Yet another 5DFEN format, designed for flexibility

Game states encoded using this format contain the following pieces of information:
- piece kinds
- piece colors
- if pieces had moved already or not
- initial board position (l, t, w)
- moves emanating from the initial boards

A board is considered initial if one of the following conditions is true:
- it does not emanate from any other board (eg. the first board in standard)
- there weren't any move from the board that it emanated that would yield that board (eg. the second board in standard - turn zero)

## Example

This is how the standard position would be encoded:

```fen
[variant "standard"]
[r*nbqk*bnr*/p*p*p*p*p*p*p*p*/8/8/8/8/P*P*P*P*P*P*P*P*/R*NBQK*BNR*;0;1;w]
```

*)

(* ## Prerequisites: *)
space = ' ' | '\n';
lowercase = ? all lowercase characters from a to z ?;
uppercase = ? all uppercase characters from A to Z ?;
number = ? 0-9 ?;
escaped_string = ? any character that may fit in a quotation-mark-delimited string, including '\"' ?;
period = '.';
underscore = '_';
dash = '-';

(* ## Root: *)
ROOT = [{HEADER}], {BOARD}, [{MOVE}];
(* Here, moves are formatted using Alexbay's notation *)
MOVE = ? a move formatted in a formalized notation ?, [{space}];
key_character = lowercase | uppercase | period | underscore | dash;
HEADER = '[', {key_character}, {space}, '"', {escaped_string}, '"]', [{space}];
BOARD = '[', fen_pieces, ';', board_layer, ';', board_time, ';', board_color, ']', [{space}];

(* ## FEN-formatted pieces *)
fen_pieces = {fen_piece | number}, {'/', {fen_piece}};
fen_piece = (piece_white | piece_black), [unmoved];

(*
This policy can be extended depending on the need for more characters.
*)
piece_white = lowercase;
piece_black = uppercase;

(*
Whether or not the piece hadn't already moved. This can safely be ignored if the piece has no special move requiring it to had moved or not.
For the standard set of pieces, the following pieces should have their "unmoved" state annotated:
- pawns
- kings
- rooks
*)
unmoved = '*';

(* ## Board coordinates: *)
board_layer = (('-' | '+'), {number}) | '0';
board_time = {number};
board_color = 'w' | 'b';
